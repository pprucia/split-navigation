apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'
apply plugin: "org.jlleitschuh.gradle.ktlint"

android {
    compileSdkVersion 28
    defaultConfig {
        applicationId "pl.prucia.test.splitnavigation"

        minSdkVersion 21
        targetSdkVersion 28
        versionCode 1
        versionName "1.0"

        vectorDrawables.useSupportLibrary = true
    }

    signingConfigs {
        snapshot {
            storeFile file("../keys/snapshot.keystore")
            storePassword "android"
            keyAlias "androiddebugkey"
            keyPassword "android"
        }
        release {
            storeFile file("../keys/release.keystore")
            // key parameters should not be kept here - to be moved
            storePassword "android"
            keyAlias "androiddebugkey"
            keyPassword "android"
        }
    }

    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'

            signingConfig signingConfigs.release
        }
        snapshot {
            // release with debug enabled
            initWith release

            debuggable true
            signingConfig signingConfigs.snapshot
        }
        staging {
            // debug with release server
            initWith debug

            applicationIdSuffix ".staging"
            versionNameSuffix "-staging"
        }
        debug {
            applicationIdSuffix ".debug"
            versionNameSuffix "-debug"
            debuggable true
            minifyEnabled false
        }
    }

    flavorDimensions "app"
    productFlavors {
        split_navigation {
            dimension "app"
            applicationId "pl.prucia.test.splitnavigation"
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    packagingOptions {
        exclude 'META-INF/main.kotlin_module'
    }
}

android.applicationVariants.all { variant ->
    variant.outputs.all {
        def buildNumber = System.getenv("CIRCLE_BUILD_NUM")
        def shaShort = System.getenv("CIRCLE_SHA1_SHORT")
        if (buildNumber != null && shaShort != null) {
            outputFileName = "${variant.flavorName}-${variant.versionName}-${buildNumber}_${shaShort}.apk"
        } else {
            outputFileName = "${variant.flavorName}-${variant.versionName}-${variant.versionCode}.apk"
        }
    }
}

android.lintOptions {
    abortOnError true
    lintConfig file("lint.xml")
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"

    implementation "com.android.support:appcompat-v7:28.0.0-beta01"
    implementation "com.android.support.constraint:constraint-layout:1.1.2"
    implementation "com.android.support:design:28.0.0-beta01"

    implementation "android.arch.lifecycle:extensions:1.1.1"
    implementation "android.arch.persistence.room:runtime:1.1.1"

    kapt "android.arch.lifecycle:compiler:1.1.1"
    kapt "android.arch.persistence.room:compiler:1.1.1"

    implementation "android.arch.navigation:navigation-fragment-ktx:1.0.0-alpha04"
    implementation "android.arch.navigation:navigation-ui-ktx:1.0.0-alpha04"
}

ktlint {
    android = true
    reporters = ["PLAIN_GROUP_BY_FILE"]
}
